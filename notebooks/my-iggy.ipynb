{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# My Iggy\n",
    "\n",
    "A simple `iggy`-like system."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 31,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "clingo version 5.4.0\n",
      "Reading from stdin\n",
      "Solving...\n",
      "Answer: 1\n",
      "node_color(fnr,-1) node_color(arcA,1) new_influence(arcA,1) new_influence(fnr,-1) new_influence(rpsP,-1) new_influence(rpmC,-1) node_color(rpsP,-1) node_color(rpmC,-1)\n",
      "Optimization: 4\n",
      "Answer: 2\n",
      "node_color(fnr,-1) node_color(arcA,1) new_influence(arcA,1) new_influence(fnr,-1) new_influence(rpsP,-1) node_color(rpsP,-1) node_color(rpmC,-1)\n",
      "Optimization: 3\n",
      "Answer: 3\n",
      "node_color(fnr,-1) node_color(arcA,1) new_influence(arcA,1) new_influence(rpsP,-1) node_color(rpsP,-1) node_color(rpmC,-1)\n",
      "Optimization: 2\n",
      "Answer: 4\n",
      "node_color(fnr,-1) node_color(arcA,1) new_influence(rpsP,-1) node_color(rpsP,-1) node_color(rpmC,-1)\n",
      "Optimization: 1\n",
      "OPTIMUM FOUND\n",
      "\n",
      "Models       : 4\n",
      "  Optimum    : yes\n",
      "Optimization : 1\n",
      "Calls        : 1\n",
      "Time         : 0.001s (Solving: 0.00s 1st Model: 0.00s Unsat: 0.00s)\n",
      "CPU Time     : 0.001s\n"
     ]
    }
   ],
   "source": [
    "%%clingo -n 0\n",
    "\n",
    "% Description of the signed directed graph.\n",
    "node(arcA; fnr; rpsP; rpmC).\n",
    "\n",
    "% Possible signs of an edge.\n",
    "sign(-1; 1).\n",
    "\n",
    "% Given edges and their sign.\n",
    "edge(arcA, fnr, -1).\n",
    "edge(arcA, rpmC, -1).\n",
    "edge(fnr, rpmC, 1).\n",
    "edge(fnr, rpsP, -1).\n",
    "\n",
    "% Deduce the input nodes.\n",
    "input(J) :- node(J), not influence(J, _).\n",
    "\n",
    "% Possible colors of a node.\n",
    "color(-1; 1).\n",
    "\n",
    "% Measurements of a node in 5 levels: -, dn, 0, up, +.\n",
    "% Taking values (-2, -1, 0, 1, 2) to simplify description of RULE 1 below.\n",
    "level(-2; -1; 0; 1; 2).\n",
    "\n",
    "% Observed nodes and their color.\n",
    "% observed(rpsP, 1).\n",
    "observed(rpsP, -1).\n",
    "observed(rpmC, -1).\n",
    "\n",
    "% Define an alias for forbidden node coloring.\n",
    ":- forbidden(N, C), node_color(N, C).\n",
    "\n",
    "% Each node can only have 1 color.\n",
    "1 {node_color(N, C): color(C)} :- node(N).\n",
    "forbidden(N, C) :- node_color(N, D), color(C), color(D), D != C.\n",
    "\n",
    "% Propagate effects forward.\n",
    "% node_color(J, C) :- node(J), color(C), not forbidden(J, C).\n",
    "\n",
    "% RULE 1: Observation satisfaction \n",
    "% For each observed node, the model behavior must match the biological observation data.\n",
    "% That is:\n",
    "\n",
    "% Where the model says should be 1, the data should >= 1;\n",
    "forbidden(N, 1) :- observed(N, L), L < 1.\n",
    "\n",
    "% Where the model says should be 0, the data should be -1 <= data <= 1;\n",
    "forbidden(N, 0) :- observed(N, L), L < -1.\n",
    "forbidden(N, 0) :- observed(N, L), L > 1.\n",
    "\n",
    "% Where the model says should be -1, the data should <= -1.\n",
    "forbidden(N, -1) :- observed(N, L), L > -1.\n",
    "\n",
    "% RULE 2: Justification of the change in one predecessor\n",
    "% For any non-input node, its change must be explained by the influence of\n",
    "% at least one of its predecessors in the graph.\n",
    "% More precisely, null changes can always occur independently of the state of\n",
    "% the predecessor nodes. However, for observed changes in node activation levels,\n",
    "% each must be explainable by an influence (with the appropriate sign) of\n",
    "% at least one predecessor.\n",
    "\n",
    "% For any non-input node J with color C, there must be\n",
    "% at least one instance of influence(J, C).\n",
    "forbidden(J, C) :- not influence(J, C), node_color(J, C), not input(J).\n",
    "\n",
    "% We define the influence of node i on node j as:\n",
    "% influence(i, j) := sign(i) * sign(edge(i, j))\n",
    "influence(I, J, P*Q) :- edge(I, J, P), node_color(I, Q).\n",
    "influence(J, C) :- influence(I, J, C).\n",
    "\n",
    "% RULE 3: Justification of zero change\n",
    "% A node can only exhibit zero change if it receives no or conflicting influences.\n",
    "% This constraint therefore assumes that each influence has an effect,\n",
    "% and that only contradictory influences can cancel each other out.\n",
    "forbidden(N, 0) :- color(C), influence(N, C), not influence(N, -C).\n",
    "\n",
    "% Repair model\n",
    "% Each new influence (+ or -) must be consistent with itself,\n",
    "% and at the same time inconsistent with the other.\n",
    "new_influence(J, C) :- not not new_influence(J, C), not new_influence(J, -C),\n",
    "    node(J), color(C).\n",
    "influence(J, C) :- new_influence(J, C).\n",
    "\n",
    "% Minimize the number of new influences (fixes) added to the graph.\n",
    "#minimize{ 1, (J, C) : new_influence(J, C) }.\n",
    "    \n",
    "#show node_color/2.\n",
    "% #show influence/2.\n",
    "#show new_influence/2."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Python 3",
   "language": "python",
   "name": "python3"
  },
  "language_info": {
   "codemirror_mode": {
    "name": "ipython",
    "version": 3
   },
   "file_extension": ".py",
   "mimetype": "text/x-python",
   "name": "python",
   "nbconvert_exporter": "python",
   "pygments_lexer": "ipython3",
   "version": "3.8.6"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 4
}
